#!/bin/tcsh -f
if( ! -e $1.pst ) then
	echo "$1.pst is missing!"
	exit
endif
@ line = `grep -n "\* model command line" $1.pst | awk 'BEGIN {FS = ":"} {print $1}'`
@ line = $line + 1
set root = `head -$line $1.pst | tail -1 | awk '{print $3}'`
echo Root: $root
@ ntpl = `head -5 $1.pst | tail -1 | awk '{print $1}'`
@ nins = `head -5 $1.pst | tail -1 | awk '{print $2}'`
@ line = `grep -n "\* model input\/output" $1.pst | awk 'BEGIN {FS = ":"} {print $1}'`
@ line = $line + $ntpl + $nins
set insline = `head -$line $1.pst | tail -1`
set infile = `echo $insline | awk '{print $1}'`
set outfile = `echo $insline | awk '{print $2}'`
set outextension = $outfile:e
echo Extension: $outextension
foreach i ( $root.*.$outextension )
	@ skip = `inschek $infile $i | grep obf | wc | awk '{print $1}'`
	if( $skip == 1 ) then
		echo Skip $i:r:e
		echo "Skip." >! $i:r\.skip
	else
		rm -f $i
	endif
end
@ loop = 1
echo $nins
while( $nins > $loop )
	@ temp = $line - $loop
	set insline = `head -$temp $1.pst | tail -1`
	set infile = `echo $insline | awk '{print $1}'`
	set outfile = `echo $insline | awk '{print $2}'`
	set outextension = $outfile:e
	echo Extension: $outextension
	foreach i ( $root.*.$outextension )
		@ skip = `inschek $infile $i | grep obf | wc | awk '{print $1}'`
		if( $skip != 1 ) then
			echo Unskip $i:r:e
			rm -f $i $i:r\.skip
		endif
	end
	foreach i ( $root.*.skip )
		set f = $i:r\.$outextension
		if( ! -e $f ) then
			echo Unskip $i:r:e
			rm -f $i
		endif
	end
	@ loop = $loop + 1
end
